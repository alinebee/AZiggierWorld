const std = @import("std");

const Program = @import("../types/program.zig");
const opcode = @import("../types/opcode.zig");

const activate_thread = @import("activate_thread.zig");
const control_threads = @import("control_threads.zig");
const set_register = @import("set_register.zig");
const copy_register = @import("copy_register.zig");

pub const Error = 
    Program.Error || 
    activate_thread.Error || 
    control_threads.Error || 
    set_register.Error || 
    copy_register.Error || 
    error {
    /// Bytecode contained an unrecognized opcode.
    UnsupportedOpcode,
};

/// A union type that wraps all possible bytecode instructions.
pub const Instruction = union(enum) {
    // TODO: see if we can codegen all this because it's going to get tiresome for 26-odd opcodes.
    ActivateThread: activate_thread.Instruction,
    ControlThreads: control_threads.Instruction,
    SetRegister: set_register.Instruction,
    CopyRegister: copy_register.Instruction,

    /// Parse the next instruction from a bytecode program and wrap it in an Instruction union tye.
    /// Returns the wrapped Instruction or an error if the bytecode could not be interpreted as an instruction.
    pub fn parse(program: *Program.Instance) Error!Instruction {
        const raw_opcode = try program.read(opcode.RawOpcode);

        return switch (opcode.parse(raw_opcode)) {
            .ActivateThread => wrap("ActivateThread", activate_thread, raw_opcode, program),
            .ControlThreads => wrap("ControlThreads", control_threads, raw_opcode, program),
            .SetRegister    => wrap("SetRegister", set_register, raw_opcode, program),
            .CopyRegister   => wrap("CopyRegister", copy_register, raw_opcode, program),
            else => error.UnsupportedOpcode,
        };
    }

    /// Parse an instruction of the specified type from the program,
    /// and wrap it in an Instruction union type initialized to the appropriate field.
    fn wrap(comptime field_name: []const u8, comptime T: type, raw_opcode: opcode.RawOpcode, program: *Program.Instance) Error!Instruction {
        return @unionInit(Instruction, field_name, try T.Instruction.parse(raw_opcode, program));
    }
};

// -- Test helpers --

/// Try to parse a literal sequence of bytecode into an Instruction union value.
fn debugParseInstruction(bytecode: []const u8) !Instruction {
    var program = Program.init(bytecode);
    return try Instruction.parse(&program);
}

/// Assert that an instruction previously generated by `parse` matches the expected union type.
/// (expectEqual won't coerce tagged unions to their underlying enum type, preventing easy comparison.)
fn expectInstructionType(expected: @TagType(Instruction), actual: @TagType(Instruction)) void {
    testing.expectEqual(expected, actual);
}

// -- Tests --

const testing = @import("../../utils/testing.zig");

test "parse returns ActivateThread instruction when given valid bytecode" {
    const instruction = try debugParseInstruction(&activate_thread.BytecodeExamples.valid);
    expectInstructionType(.ActivateThread, instruction);
}

test "parse returns ControlThreads instruction when given valid bytecode" {
    const instruction = try debugParseInstruction(&control_threads.BytecodeExamples.valid);
    expectInstructionType(.ControlThreads, instruction);
}

test "parse returns SetRegister instruction when given valid bytecode" {
    const instruction = try debugParseInstruction(&set_register.BytecodeExamples.valid);
    expectInstructionType(.SetRegister, instruction);
}

test "parse returns CopyRegister instruction when given valid bytecode" {
    const instruction = try debugParseInstruction(&copy_register.BytecodeExamples.valid);
    expectInstructionType(.CopyRegister, instruction);
}

test "parse returns UnsupportedOpcode error when it encounters an unknown opcode" {
    const bytecode = [_]u8{ 0xFF }; // unknown opcode
    testing.expectError(error.UnsupportedOpcode, debugParseInstruction(&bytecode));
}